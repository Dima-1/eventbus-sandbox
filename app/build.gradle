apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'jacoco'

def unitTest='testQaDebugUnitTest'
def coverageReport='createQaDebugCoverageReport'
task jacocoTestReport(type: JacocoReport, dependsOn: [unitTest,coverageReport ]) {

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*']
    def javaTree = fileTree(dir: "$project.buildDir/intermediates/classes/qa", excludes: fileFilter)
    def kotlinTree = fileTree(dir: "$project.buildDir/tmp/kotlin-classes/qaDebug", excludes: fileFilter)
    def mainSrc = "$project.projectDir/src/main/java"

    sourceDirectories = files([mainSrc])
    classDirectories = files([javaTree,kotlinTree])
    executionData = fileTree(dir: project.buildDir, includes: [
            'jacoco/*.exec', 'outputs/code-coverage/connected/**/*coverage.ec'
    ])
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}
android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.example.user.eventest"
        minSdkVersion 18
        targetSdkVersion 27
        versionCode 1
        versionName "0.4"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":"$projectDir/schemas".toString()]
            }
        }
    }

    buildToolsVersion '27.0.3'
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    testOptions {
        animationsDisabled = true
    }
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled = true
        }
    }

    flavorDimensions "default"
    productFlavors {
        qa {
            resValue "string", "app_name", "Memos-qa"
            applicationIdSuffix ".qa"
            versionNameSuffix "-qa"
        }
        prod{
            resValue "string", "app_name", "Memos"
        }
    }
}

dependencies {

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:27.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.android.support:design:27.1.0'
    implementation 'android.arch.persistence.room:runtime:1.0.0'
    implementation 'org.greenrobot:eventbus:3.1.1'
    implementation 'com.jakewharton:butterknife:8.8.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    kapt 'com.jakewharton:butterknife-compiler:8.8.1'
    kapt 'android.arch.persistence.room:compiler:1.0.0'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.13.0'
    testImplementation 'org.mockito:mockito-inline:2.13.0'
    androidTestImplementation "org.mockito:mockito-android:2.13.0"
    androidTestImplementation 'android.arch.persistence.room:testing:1.0.0'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-contrib:3.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-intents:3.0.1'
    androidTestImplementation 'com.android.support.test.uiautomator:uiautomator-v18:2.1.3'
}
repositories {
    mavenCentral()
}
